apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

checkstyle {
    ignoreFailures = true
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
}

task checkstyleAndroid(type: Checkstyle) {
    ignoreFailures = true
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")

    source 'src'
    include '**/*.java'
    exclude '**/gen/**', '**/test/**'

    classpath = files()
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled true 
    }
}

tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ") && !checkstyleTask.ignoreFailures) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

ext.getVersionName = { ->
    try {
        def sout = new StringBuilder()
        def serr = new StringBuilder()
        def proc = 'make travis-version'.execute()
        proc.consumeProcessOutput(sout, serr)
        proc.waitForOrKill(1000)
        return sout.trim() ?: serr.replaceAll("\\s", " ")
    } catch (ignored) {
        return "0.0"
    }
}

int MILLIS_IN_MINUTE = 1000 * 60
int minutesSinceEpoch = System.currentTimeMillis() / MILLIS_IN_MINUTE
int genericVersionCode = minutesSinceEpoch
String versionNameExt = getVersionName()

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    flavorDimensions "default"
    
    defaultConfig {
        applicationId "ru.sibur.android.garbagecollector"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode genericVersionCode
        versionName versionNameExt
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
        
    productFlavors {
        dev {
            minSdkVersion 21
            versionNameSuffix "-dev"
            applicationIdSuffix '.dev'
        }
    }
    
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.mergedFlavor.versionCode = genericVersionCode;
            variant.mergedFlavor.versionName = genericVersionCode + "-" + variant.flavorName;
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'net.sourceforge.streamsupport:android-retrostreams:1.6.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
